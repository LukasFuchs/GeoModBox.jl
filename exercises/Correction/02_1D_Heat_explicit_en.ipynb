{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Solving the 1-D Diffusion Equation (Explicit)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## The Problem\n",
    "\n",
    "We are interested in how the temperature $T$ changes with time $t$, for a given initial temperature $T_{0}[x,t_{0}]$ and boundary conditions; e.g., a dike intrusion in the lithosphere, with \n",
    "- a total length $L$ = 100 m, \n",
    "- a dike width $W$ = 5 m, \n",
    "- a thermal diffusivity $\\kappa$ = 10⁻⁶ m²/s, \n",
    "- a dike temperature $T_{dike}$ = 1200 °C, and \n",
    "- a background temperature $T_{background}$ = 300 °C.\n",
    "\n",
    "<img src=\"../Figures/Exercise02_1b.png\" alt=\"drawing\" width=\"350\"/> <br>\n",
    "**Fig. 1.** Sketch of the geological problem and the profile of the initial temperature condition.\n",
    "\n",
    "How long would it take for the dike to cool to a given temperature? How does the solution behave over time? Is the temperature of the host rock important? How does the solution depend on the chosen numerical parameters (e.g., the number of grid points or the time step size)?\n",
    "\n",
    "To determine the cooling time, we need to solve the temperature diffusion equation (a parabolic partial differential equation (PDE)):\n",
    "\n",
    "$$\n",
    "\\rho c_p \\frac{\\partial{T}}{\\partial{t}} = \\frac{\\partial{}}{\\partial{x}} \\left( k\\frac{\\partial{T}}{\\partial{x}} \\right), \\tag{1}\n",
    "$$\n",
    "\n",
    "where $k$ is the thermal conductivity [W/m/K], $T$ is the temperature [K], and $\\rho$ is the density [kg/m³].\n",
    "\n",
    "Assuming the thermal parameters are constant, we can rewrite the equation and express it using the thermal diffusivity [m²/s] $\\kappa = \\frac{k}{\\rho c_p}$:\n",
    "\n",
    "$$\n",
    "\\frac{\\partial{T}}{\\partial{t}} = \\kappa \\frac{\\partial^2{T}}{\\partial{x^2}}. \\tag{2}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## The Solution"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Finite Difference Approximation\n",
    "\n",
    "To solve the problem numerically using finite differences, we first need to create a numerical grid (the discretization):\n",
    "\n",
    "<img src=\"../Figures/Exercise02_2.png\" alt=\"drawing\" width=\"600\"/> <br>\n",
    "**Fig. 2.** Numerical 1-D grid for the discretization of the *PDE*.\n",
    "\n",
    "To solve the diffusion equation numerically using the explicit finite difference scheme, we now need to rewrite the *PDE* using finite difference expressions.\n",
    "\n",
    "Forward in time (FT):\n",
    "$$\n",
    "\\frac{\\partial{T}}{\\partial{t}} \\approx \\frac{T_{I^\\textrm{C}}^{n+1}-T_{I^\\textrm{C}}^{n}}{t^{n+1}-t^{n}} = \\frac{T_{I^\\textrm{C}}^{n+1}-T_{I^\\textrm{C}}^{n}}{\\Delta t} \\tag{3}\n",
    "$$\n",
    "\n",
    "Central in space (CS):\n",
    "$$\n",
    "\\frac{\\partial^2{T}}{\\partial{x^2}} \\approx \\frac{\\frac{T_{I^\\textrm{E}}^{n}-T_{I^\\textrm{C}}^{n}}{\\Delta x} - \\frac{T_{I^\\textrm{C}}^{n}-T_{I^\\textrm{W}}^{n}}{\\Delta x}}{\\Delta x} =\n",
    "\\frac{T_{I^\\textrm{E}}^{n} - 2T_{I^\\textrm{C}}^{n} + T_{I^\\textrm{W}}^{n}}{(\\Delta x)^2} \\tag{4}\n",
    "$$\n",
    "\n",
    "Thus, in explicit form, the temperature at each point at the new time is given by:\n",
    "$$\n",
    "T_{I^\\textrm{C}}^{n+1} = T_{I^\\textrm{C}}^{n} + \\kappa \\Delta t \\left( \\frac{T_{I^\\textrm{E}}^{n}-2T_{I^\\textrm{C}}^{n}+T_{I^\\textrm{W}}^{n}}{\\Delta x^2} \\right) \\tag{5}\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Let us now first load the necessary modules for the numerical solution of our problem. We will begin by programming the solvers ourselves, before making use of the predefined solvers in `GeoModBox.jl`. The predefined solver is located in the submodule `GeoModBox.HeatEquation.OneD`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "using GeoModBox.HeatEquation.OneD\n",
    "a = time()"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Parameter Definitions\n",
    "\n",
    "Let us first define the parameters for the problem (physical constants, numerical domain, and time parameters).\n",
    "\n",
    "The parameter `alternative` describes different programming approaches:\n",
    "\n",
    "1. Fully explicit implementation (loop)\n",
    "2. Fully explicit implementation (index array)\n",
    "3. Use of the built-in function"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "alternative =   3\n",
    "# Physics --------------------------------------------------------------- #\n",
    "L           =   100.0       # Length [m]\n",
    "Tdike       =   1200.0      # Dike temperature [C]\n",
    "Trock       =   300.0       # Background temperature [C]\n",
    "κ           =   1.0e-6      # Thermal Diffusivity [m2/s]\n",
    "W           =   5.0         # Dike width [m]\n",
    "# ----------------------------------------------------------------------- #\n",
    "# Numerical Parameter --------------------------------------------------- #\n",
    "nc          =   100                 # Number of centroids \n",
    "Δx          =   L/(nc)              # Grid spacing \n",
    "xc          =   Δx/2:Δx:(L-Δx/2)    # x-coordinates of the centroids  \n",
    "ind         =   1:nc                # Indexes for internal points\n",
    "# ----------------------------------------------------------------------- #\n",
    "# Time Parameter -------------------------------------------------------- #\n",
    "day         =   3600.0*24.0             # Seconds per day\n",
    "Δt          =   0.9 * Δx^2 / (2.0 * κ)\n",
    "nt          =   floor(Int, 200.0 * day/Δt ) \n",
    "Time        =   0.0\n",
    "# ----------------------------------------------------------------------- #"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Initial Conditions\n",
    "\n",
    "To solve our problem, we first need to define the initial conditions. We assume 300 °C for the host rock and 1200 °C for the dike. That is, the initial temperature is defined by:\n",
    "\n",
    "$$\n",
    "T \\left(x < \\left( \\frac{L}{2} - W \\right), x > \\left( \\frac{L}{2} + W \\right), t = 0 \\right) = 300, \\tag{6}\n",
    "$$\n",
    "\n",
    "$$\n",
    "T \\left(x > \\left( \\frac{L}{2} - W \\right), x < \\left( \\frac{L}{2} + W \\right), t = 0 \\right) = 1200. \\tag{7}\n",
    "$$\n",
    "\n",
    "Next, we will first visualize the initial condition graphically."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Initial Condition; Temperature Profile -------------------------------- #\n",
    "T       =   (\n",
    "    T       =   zeros(nc), \n",
    "    T_ex    =   zeros(nc+2),\n",
    ")\n",
    "T.T     .=  Trock                       # Background T\n",
    "@. T.T[abs(xc-L/2) <= W/2] =  Tdike     # Dike T\n",
    "\n",
    "T.T_ex[2:end-1]     .=  T.T\n",
    "\n",
    "p = plot(xc, T.T, xlabel=\"x [m]\", ylabel=\"T [°C]\", \n",
    "            title=\"Temperature distribution after \n",
    "            $(round(Time/day, digits=1)) days\", \n",
    "            xlim=(0,L), ylim=(0, Tdike))\n",
    "display(p)\n",
    "# ----------------------------------------------------------------------- #"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Boundary Conditions\n",
    "\n",
    "Since we use cell-centered grid points (centroids) for the temperature, no grid point (vertex) lies directly on the boundaries (contrary to expectations, this is actually an advantage!). To prescribe the temperature boundary conditions, we therefore need to make use of additional *ghost nodes* (see Figure 2), i.e., we determine the temperature on the *ghost nodes* in order to be able to solve the temperature partial differential equation at the **nearest interior** grid point using finite differences.\n",
    "\n",
    "For a constant temperature boundary condition at the boundaries (Dirichlet), we can determine the temperature on the *ghost nodes* by linear interpolation such that:\n",
    "\n",
    "$$\n",
    "T_{Ghost}^W = 2 T_{BC}^W - T_1 \\tag{8}\n",
    "$$\n",
    "\n",
    "$$\n",
    "T_{Ghost}^E = 2 T_{BC}^E - T_{nc} \\tag{9}\n",
    "$$\n",
    "\n",
    "In the implementation, we make use of a small trick and define the temperature on the *ghost nodes* later in the script. For this purpose, we first define a *tuple* that contains the type and the value of the temperature boundary conditions.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Boundary Conditions --------------------------------------------------- #\n",
    "BC          =   (\n",
    "                    type = (W=:Dirichlet, E=:Dirichlet),\n",
    "                    #type = (W=:Neumann, E=:Neumann),\n",
    "                    val = (W=:300.0,E=:300.0)\n",
    ")\n",
    "# ----------------------------------------------------------------------- #"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Visualization\n",
    "\n",
    "To visualize the results as an animation in a GIF file, we first need to specify the location and the name of the file:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Animationssettings ---------------------------------------------------- #\n",
    "path        =   string(\"./Results/\")\n",
    "anim        =   Plots.Animation(path, String[] )\n",
    "filename    =   string(\"02_1D_explicit_\",alternative)\n",
    "save_fig    =   1\n",
    "# ----------------------------------------------------------------------- #"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Time Loop\n",
    "\n",
    "Numerically, we can now solve the *ODE* in a time loop in different ways (Alternative I is sufficient; those interested can also implement Alternatives II and III). At each time step, the temperature at the ghost nodes is determined using the equations above."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Time loop ------------------------------------------------------------- #\n",
    "for n = 1:nt\n",
    "    println(\"Time Step: \",n,\", Time: $(round(Time/day, digits=1)) [d]\")        \n",
    "\n",
    "    if alternative == 1       \n",
    "        # Alternative I ---\n",
    "        # Programming a loop over the grid (without boundary nodes)\n",
    "        # Define the boundary conditions\n",
    "        # West\n",
    "        T.T_ex[1]    =   (BC.type.W==:Dirichlet) * (2 * BC.val.W - T.T_ex[2]) + \n",
    "                                (BC.type.W==:Neumann) * (T.T_ex[2] - BC.val.W*Δx)\n",
    "        # East\n",
    "        T.T_ex[end]  =   (BC.type.W==:Dirichlet) * (2 * BC.val.E - T.T_ex[nc+1]) +\n",
    "                                (BC.type.W==:Neumann) * (T.T_ex[nc+1] + BC.val.E*Δx)\n",
    "        for i = 1:nc\n",
    "            # Calculate temperature at point i for the new time step\n",
    "            T.T[i]    =   T.T_ex[i+1] + κ * Δt * \n",
    "                                    (T.T_ex[i + 2] - 2.0 * T.T_ex[i+1] + T.T_ex[i]) / Δx^2\n",
    "        end\n",
    "        T.T_ex[2:end-1]  .=  T.T\n",
    "    elseif alternative == 2\n",
    "        # Alternative II ---                         \n",
    "        # West\n",
    "        T.T_ex[1]    =   (BC.type.W==:Dirichlet) * (2 * BC.val.W - T.T_ex[2]) + \n",
    "                                (BC.type.W==:Neumann) * (T.T_ex[2] - BC.val.W*Δx)\n",
    "        # East\n",
    "        T.T_ex[end]  =   (BC.type.W==:Dirichlet) * (2 * BC.val.E - T.T_ex[nc+1]) +\n",
    "                                (BC.type.W==:Neumann) * (T.T_ex[nc+1] + BC.val.E*Δx)\n",
    "\n",
    "        @. T.T[ind]  =   T.T_ex[ind+1] + κ * Δt * \n",
    "                                    (T.T_ex[ind + 2] - 2.0 * T.T_ex[ind+1] + T.T_ex[ind]) / Δx^2\n",
    "        T.T_ex[2:end-1]  .=  T.T\n",
    "    elseif alternative == 3            \n",
    "        ForwardEuler1Dc!( T, κ, Δx, Δt, nc, BC )\n",
    "    end\n",
    "    # Calculate time ---\n",
    "    Time    =   Time + Δt\n",
    "    # Plot Solution ---\n",
    "    p = plot(xc, T.T, xlabel=\"x [m]\", ylabel=\"T [°C]\", \n",
    "            title=\"Temperatur distribution after\n",
    "            $(round(Time/day, digits=1)) days\", \n",
    "            xlim=(0,L),ylim=(0, Tdike))    \n",
    "    if save_fig == 1\n",
    "        Plots.frame(anim)\n",
    "    else\n",
    "        display(p)\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We now need to create and save the animation:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Save Animation -------------------------------------------------------- #\n",
    "if save_fig == 1\n",
    "    # Write the frames to a GIF file\n",
    "    Plots.gif(anim, string( path, filename, \".gif\" ), fps = 15)\n",
    "else\n",
    "    display(p)\n",
    "end\n",
    "foreach(rm, filter(startswith(string(path,\"00\")), readdir(path,join=true)))\n",
    "# ----------------------------------------------------------------------- #\n",
    "b = time()\n",
    "println(b-a)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.4",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
